/*
 * SPDX-FileCopyrightText: 2013 microG Project Team
 * SPDX-License-Identifier: Apache-2.0
 */

buildscript {
    ext.applicationNamespace = "com.google.android.gms"
    ext.basePackageName = "app.revanced"

    ext.annotationVersion = '1.8.2'
    ext.appcompatVersion = '1.7.0'
    ext.biometricVersion = '1.1.0'
    ext.coreVersion = '1.13.1'
    ext.fragmentVersion = '1.8.2'
    ext.lifecycleVersion = '2.8.4'
    ext.loaderVersion = '1.1.0'
    ext.materialVersion = '1.12.0'
    ext.mediarouterVersion = '1.7.0'
    ext.multidexVersion = '2.0.1'
    ext.navigationVersion = '2.7.7'
    ext.preferenceVersion = '1.2.1'
    ext.recyclerviewVersion = '1.3.2'
    ext.webkitVersion = '1.11.0'

    ext.coroutineVersion = '1.8.1'
    ext.kotlinVersion = '2.0.0'

    ext.cronetVersion = '119.6045.31'
    ext.okhttpVersion = '4.12.0'
    ext.slf4jVersion = '2.0.16'
    ext.volleyVersion = '1.2.1'
    ext.wearableVersion = '0.1.1'
    ext.wireVersion = '5.0.0'

    ext.androidBuildGradleVersion = '8.5.1'

    ext.androidBuildVersionTools = '34.0.0'

    ext.androidMinSdk = 24
    ext.androidTargetSdk = 29
    ext.androidCompileSdk = 34

    ext.localProperties = new Properties()

    ext.includeProjects = [
            ':play-services-base-core',
            ':play-services-cast-core',
            ':play-services-cast-framework-core',
            ':play-services-conscrypt-provider-core',
            ':play-services-core-proto', // deprecated
            ':play-services-core:microg-ui-tools',
            ':play-services-api',
            ':play-services-tasks-ktx',
            ':play-services-auth'
    ]

    try {
        var stream = rootProject.file('local.properties').newDataInputStream()
        ext.localProperties.load(stream)
        stream.close()
    } catch (ignored) {
        // Ignore
    }

    ext.hasModule = (String name, boolean enabledByDefault) -> {
        return ext.localProperties.getProperty("modules." + name, enabledByDefault.toString()).toBoolean()
    }

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$androidBuildGradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.squareup.wire:wire-gradle-plugin:$wireVersion"
    }
}

allprojects {
    apply plugin: 'idea'

    group = 'org.microg.gms'
    ext.appVersionName = 5.9
    ext.appVersionCode = 243864000
    ext.baseVersion = ext.appVersionCode.toString()[0..1] + '.' + ext.appVersionCode.toString()[2..3] + '.' + ext.appVersionCode.toString()[4..5]
    version = ext.appVersionName+" GMS-"+ext.baseVersion.replaceAll("\\.", "")
    ext.isReleaseVersion = false
}

subprojects {
    repositories {
        mavenCentral()
        google()
    }
}
